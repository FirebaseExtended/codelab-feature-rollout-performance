// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath 'com.google.gms:google-services:4.3.10'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.27.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }

    def isNonStable = { candidate -> 
        return ['alpha', 'beta', 'rc', 'SNAPSHOT'].any { word ->
            return candidate.version.contains(word)
        }
    }

    def isDenyListed = { candidate ->
        def denylist = [
            'androidx.browser:browser'
        ]
        return denylist.any { word ->
            return candidate.toString().contains(word)
        }
    }

    dependencyUpdates {
        rejectVersionIf {
            isNonStable(it.candidate) || isDenyListed(it.candidate)
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
